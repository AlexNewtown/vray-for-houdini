#
# Copyright (c) 2015, Chaos Software Ltd
#
# V-Ray For Houdini
#
# Andrei Izrantcev <andrei.izrantcev@chaosgroup.com>
#
# All rights reserved. These coded instructions, statements and
# computer programs contain unpublished information proprietary to
# Chaos Group Ltd, which is protected by the appropriate copyright
# laws and may not be disclosed to third parties or copied or
# duplicated, in whole or in part, without prior written consent of
# Chaos Group Ltd.
#

# Made using: cat sesitag.txt | sesitag -m
set(PLUGIN_TAGINFO "\"3262197cbf104d152da5089a671b9ff8394bdcd9d530d8aa27f5984e1714bfd251aa2487851869344346dba5159b681c2da1a710878dac641a5874f82bead6fb0cb006e8bedd1ad3f169d85849f95eb181\"")

include(CheckIncludeFile)

include(vfh_compiler)
include(vfh_macro)
include(vfh_houdini)
include(vfh_vrayappsdk)
include(vfh_vraysdk)

add_definitions(-DVRAY_FOR_HOUDINI)

include_directories(.)
include_directories(ui)
include_directories(op)
include_directories(sop)
include_directories(vop)
include_directories(vop/brdf)
include_directories(vop/material)
include_directories(vop/texture)
include_directories(vop/uvwgen)
include_directories(vop/meta)
include_directories(export)
include_directories(export/utils)
include_directories(utils)

file(GLOB_RECURSE SOURCES "*.cpp")
file(GLOB_RECURSE HEADERS "*.h")

# *.aur preview
CHECK_INCLUDE_FILE(FluidInterface.h HAS_PHOENIX)
if (HAS_PHOENIX)
	add_definitions(-DCGR_HAS_AUR)
endif()

# *.vrscene preview
CHECK_INCLUDE_FILE(vrayscene_preview_mesh.h HAS_VRSCENE)
if (HAS_VRSCENE)
	add_definitions(-DCGR_HAS_VRAYSCENE)
endif()

use_houdini_sdk()
use_vray_sdk()
use_vray_appsdk()

houdini_plugin(${PROJECT_NAME} "${SOURCES};${HEADERS}")

link_with_vray_sdk(${PROJECT_NAME})
link_with_vray_appsdk(${PROJECT_NAME})
link_with_boost(${PROJECT_NAME})

# Install shelf and icon locally
#
file(GLOB VFH_PY_SCRIPTS ${CMAKE_SOURCE_DIR}/deploy/scripts/*.py)

install(FILES       ${CMAKE_SOURCE_DIR}/deploy/vfh.shelf
		DESTINATION ${HOUDINI_HOME_PATH}/toolbar)

install(FILES       ${CMAKE_SOURCE_DIR}/deploy/ROP_vray.svg
		DESTINATION ${HOUDINI_HOME_PATH}/config/Icons)

install(FILES       ${VFH_PY_SCRIPTS}
		DESTINATION ${HOUDINI_HOME_PATH}/scripts/vfh)

# Install to local share
#
option(USE_LOCAL_INSTALL "Install to local share" OFF)
if(USE_LOCAL_INSTALL)
	set(VFH_LOCAL_INSTALL_PATH "" CACHE PATH "Local install root path")
	if(${VFH_LOCAL_INSTALL_PATH} STREQUAL "")
		message(FATAL_ERROR "USE_LOCAL_INSTALL is ON, but VFH_LOCAL_INSTALL_PATH is not set!")
	else()
		cgr_install_runtime(${PROJECT_NAME} ${VFH_LOCAL_INSTALL_PATH}/dso)

		install(FILES       ${CMAKE_SOURCE_DIR}/deploy/vfh.shelf
				DESTINATION ${VFH_LOCAL_INSTALL_PATH}/deploy)

		install(FILES       ${CMAKE_SOURCE_DIR}/deploy/ROP_vray.svg
				DESTINATION ${VFH_LOCAL_INSTALL_PATH}/deploy)

		install(FILES       ${CMAKE_SOURCE_DIR}/deploy/hfs.bat
				DESTINATION ${VFH_LOCAL_INSTALL_PATH})

		install(FILES       ${CMAKE_SOURCE_DIR}/INSTALL.txt
				DESTINATION ${VFH_LOCAL_INSTALL_PATH})
	endif()
endif()

# Create program launcher with all needed environment variables
#
option(USE_LAUNCHER "Create launcher" OFF)
if(USE_LAUNCHER)
	if(WIN32)
		set(HFS_RUN_FILE "$ENV{USERPROFILE}/Desktop/hfs.bat")

		message(STATUS "Generating Houdini run file (${HFS_RUN_FILE})")

		file(WRITE ${HFS_RUN_FILE}
			":: NOTE: This file is automatically generated!\n"
			"\n"
			"set VRAY_PLUGIN_DESC_PATH=${CMAKE_SOURCE_DIR}/deploy/plugins_desc\n"
			"\n"
			"set HOUDINI13_VOLUME_COMPATIBILITY=1\n"
			"set HFS=${HOUDINI_INSTALL_ROOT}\n"
			"set VRAY_SDK=${APPSDK_ROOT}\n"
			"set VRAY_PATH=%VRAY_SDK%/bin\n"
			"set PATH=%HFS%/bin:%VRAY_PATH%:%PATH%\n"
			"set HOUDINI_DSO_ERROR=1\n"
			"\n"
			"\"%HOUDINI_INSTALL_ROOT%/bin/houdini.exe\" -foreground %*\n"
		)
	else()
		set(HFS_RUN_FILE "$ENV{HOME}/bin/hfs")
		set(HFS_ENV_FILE "$ENV{HOME}/bin/hfs.env")

		message(STATUS "Generating Houdini run file (${HFS_RUN_FILE})")

		file(WRITE ${HFS_ENV_FILE}
			"# NOTE: This file is automatically generated!\n"
			"\n"
			"export VRAY_PLUGIN_DESC_PATH=${CMAKE_SOURCE_DIR}/deploy/plugins_desc\n"
			"\n"
			"export HOUDINI13_VOLUME_COMPATIBILITY=1\n"
			"export HFS=${HOUDINI_INSTALL_ROOT}\n"
			"export VRAY_SDK=${APPSDK_ROOT}\n"
			"export LD_LIBRARY_PATH=$VRAY_SDK/bin\n"
			"export VRAY_PATH=$VRAY_SDK/bin\n"
			"export PATH=$HFS/bin:$VRAY_PATH:$PATH\n"
			"export HOUDINI_DSO_ERROR=1\n"
		)

		file(WRITE ${HFS_RUN_FILE}
			"# NOTE: This file is automatically generated!\n"
			"\n"
			"source ${HFS_ENV_FILE}\n"
			"\n"
			"${HOUDINI_INSTALL_ROOT}/bin/houdini -foreground $*\n"
		)
	endif()
endif()
